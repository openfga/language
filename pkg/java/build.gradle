plugins {
    id 'java'

    // Quality
    id 'jacoco'
    id 'jvm-test-suite'
    id 'com.diffplug.spotless' version '6.25.0'

    // IDE
    id 'idea'
    id 'eclipse'

    // Publishing
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
}

//apply from: 'publish.gradle'

group = 'dev.openfga'
version = '0.0.1'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    withJavadocJar()
    withSourcesJar()
}

sourceSets {
    main {
        java {
            srcDirs = [
                    'src/main/java',
                    'src/main/gen',
            ]
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

javadoc {
    exclude("dev/openfga/language/antlr/**")

    // Ignore warnings.
    options.addStringOption('Xdoclint:none', '-quiet')
}

test {
    useJUnitPlatform()
    // JaCoCo coverage report is always generated after tests run.
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    // tests are required to run before generating a JaCoCo coverage report.
    dependsOn test
}

ext {
    junit_version = "5.10.1"
}

dependencies {
    implementation 'org.antlr:antlr4:4.13.1'
    implementation 'dev.openfga:openfga-sdk:0.4.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.1'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.15.3'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
}

spotless {
    enforceCheck false
    format 'misc', {
        // define the files (e.g. '*.gradle', '*.md') to apply `misc` to
        target '.gitignore', '*.gradle'
        // define the steps to apply to those files
        trimTrailingWhitespace()
        indentWithSpaces() // Takes an integer argument if you don't like 4
        endWithNewline()
    }
    java {
        targetExclude 'src/main/gen/' // ignore generated files

        palantirJavaFormat()
        removeUnusedImports()
        importOrder()
    }
}

tasks.register('fmt') {
    dependsOn 'spotlessApply'
}

tasks.register('lint') {
    dependsOn 'spotlessCheck'
}
